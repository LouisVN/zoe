buildscript {
    repositories {
        mavenLocal()
        jcenter()
    }

    dependencies {
        classpath "com.github.jengelman.gradle.plugins:shadow:4.0.4"
    }
}

plugins {
    id 'org.jetbrains.kotlin.jvm' version "1.3.70" apply false
    id 'com.github.johnrengelman.shadow' version "4.0.4" apply false
    id 'com.google.cloud.tools.jib' version '2.0.0' apply false
    id 'com.palantir.git-version' version '0.12.2'
}

allprojects {
    group 'com.adevinta.oss'

    /**
     * Assumes tags have the format of v*.*.*
     * strips the starting 'v' character to make the version number compatible with '.deb' packages
     */
    version versionDetails().lastTag.replaceAll("^v", "")

    repositories {
        mavenLocal()
        jcenter()
        mavenCentral()
        maven { url "https://packages.confluent.io/maven/" }
    }
}

subprojects {
    apply plugin: 'org.jetbrains.kotlin.jvm'
    apply plugin: 'com.github.johnrengelman.shadow'
    apply plugin: 'maven-publish'

    sourceCompatibility = 11

    dependencies {
        implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk8"
    }
}

wrapper {
    gradleVersion = '6.2.2'
}